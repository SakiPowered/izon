plugins {
    id 'org.cadixdev.licenser' version '0.6.1' apply false
    id 'maven-publish'
}

group('gg.saki')
version('1.0.0-SNAPSHOT')
description('A Java library for runtime dependency management')


allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'java-library'

    dependencies {
        compileOnlyApi('org.jetbrains:annotations:24.0.1')
    }


    license {
        header = project.rootProject.file('LICENSE')

        include '**/*.java'
        charset = this.properties['textEncoding']
        lineEnding = '\n'

        style {
            java = 'BLOCK_COMMENT'
        }
    }


    tasks.withType(JavaCompile).configureEach {
        options.encoding = this.properties['textEncoding']
        options.compilerArgs += ['-parameters', '-Xlint:all']
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            // TODO: Add javadoc & sources jars. And add saki repo and fill out the rest of the pom info.

            pom {
                name = 'izon'
                description = this.description
                url = 'https://github.com/SakiPowered/izon'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/SakiPowered/izon.git'
                    developerConnection = 'scm:git:ssh://github.com/SakiPowered/izon.git'
                    url = 'https://github.com/SakiPowered/izon'
                }
            }
        }
    }
}